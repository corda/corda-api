plugins {
    id 'groovy-gradle-plugin'
}

repositories {
    gradlePluginPortal()
}

// We can't get the values directly from `gradle.properties` from here for some gradle-y reason.
// So we'll load it into our own object to grab what we need.
def constants = new Properties()
file("$rootDir/../gradle.properties").withInputStream { InputStream input -> constants.load(input) }
def internalPluginVersion = constants.getProperty('internalPluginVersion')
def artifactoryContextUrl = constants.getProperty('artifactoryContextUrl')

repositories {
        def cordaUseCache = System.getenv("CORDA_USE_CACHE")
        if (cordaUseCache != null) {
            maven {
                url = "$artifactoryContextUrl/$cordaUseCache"
                name = "R3 Maven remote repositories"
                authentication {
                    basic(BasicAuthentication)
                }
                credentials {
                    username = findProperty('cordaArtifactoryUsername') ?: System.getenv('CORDA_ARTIFACTORY_USERNAME')
                    password = findProperty('cordaArtifactoryPassword') ?: System.getenv('CORDA_ARTIFACTORY_PASSWORD')
                }
            }
        } else {

            maven {
                url "$artifactoryContextUrl/engineering-tools-maven"
                authentication {
                    basic(BasicAuthentication)
                }
                credentials {
                    username = findProperty('cordaArtifactoryUsername') ?: System.getenv('CORDA_ARTIFACTORY_USERNAME')
                    password = findProperty('cordaArtifactoryPassword') ?: System.getenv('CORDA_ARTIFACTORY_PASSWORD')
                }
                content {
                    includeGroupByRegex 'com\\.r3\\.internal(\\..*)?'
                }
            }
        }
        gradlePluginPortal()

}

dependencies {
    constraints {
        implementation libs.bnd.lib
        implementation libs.bnd.embedded.repo
        implementation libs.bnd.resolve
    }
    implementation libs.bnd.gradle

    if (System.getenv('CORDA_ARTIFACTORY_USERNAME') != null || project.hasProperty('cordaArtifactoryUsername')) {
        implementation "com.r3.internal.gradle.plugins:publish:$internalPluginVersion"
        implementation "com.r3.internal.gradle.plugins:api-scanner:$internalPluginVersion"
    }

}
