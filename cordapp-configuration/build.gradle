import static org.gradle.api.JavaVersion.VERSION_1_8

plugins {
    id 'java-gradle-plugin'
    id 'java-library'
}

description 'Configuration properties for cordapp-cpk Gradle plugin.'

group = 'net.corda.cordapp'

gradlePlugin {
    plugins {
        cordappConfiguration {
            id = 'net.corda.cordapp.cordapp-configuration'
            implementationClass = 'net.corda.cordapp.ConfigurationPlugin'
        }
    }
    automatedPublishing = false
}

java {
    // Allowing this plugin to run successfully
    // on Java 8 means that Corda can fail the
    // build later with a graceful error message.
    sourceCompatibility = VERSION_1_8
    targetCompatibility = VERSION_1_8

    withJavadocJar()
    withSourcesJar()
}

dependencies {
    testImplementation libs.bundles.test
    testRuntimeOnly libs.bundles.test.runtime
}

tasks.named('jar', Jar) {
    manifest {
        attributes('Sealed': true)
    }
}

tasks.withType(Test).configureEach {
    useJUnitPlatform()
}

tasks.withType(ValidatePlugins).configureEach {
    // Ask Gradle to tell us how to annotate tasks correctly.
    enableStricterValidation = true
}
