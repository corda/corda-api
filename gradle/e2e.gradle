
tasks.register('prepareBootstrapper', Copy) {
    group 'Preparing for e2e/smoke tests execution'
    description 'Copy the OSGi bootstrapper into build/testDeployment/bootstrapper'
    dependsOn(':tools:bootstrapper:jar')
    from tasks.getByPath(':tools:bootstrapper:jar')
    into "${buildDir}/testDeployment/bootstrapper"

    doFirst {
        delete "${buildDir}/testDeployment/bootstrapper"
    }
}

tasks.register('prepareApps', Copy) {
    group 'Preparing for e2e/smoke tests execution'
    description 'Copy the CorDapp jars into build/testDeployment/cordapps'

    from subprojects.findAll {
        it.path.startsWith(":testing")
    }.collect {
        it.tasks.withType(Jar)
                .matching { jar ->
                    jar.name == 'cpk'
                }
    }
    into "${buildDir}/testDeployment/cordapps"

    doFirst {
        delete "${buildDir}/testDeployment/cordapps"
    }
}

tasks.register('prepareDeployment') {
    group 'Preparing for e2e/smoke tests execution'
    description 'Copy all the needed jars for OSGi e2e tests into build/testDeployment'
    dependsOn('prepareBootstrapper',
            'prepareApps')
}
